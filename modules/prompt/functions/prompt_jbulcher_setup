#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nrGV6pg.png
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_jbulcher_color_path {
  local c
  typeset -A c
  local thepath="${1}"
  # %F{208} orange
  # %F{112} green
  print "%F{112}${thepath//\//%F{208\}/%F{112\}}"
}

prompt_jbulcher_git_info() {
  local c
  typeset -A c
  if (( _prompt_jbulcher_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_jbulcher_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_jbulcher_precmd_async_data"
      if [[ 0 -ne $#git_info ]]; then
        # %F{208} orange
        PROMPT="${JBPROMPT}
${(e)git_info[prompt]} ${git_path}%F{208}± "
      fi
      unalias typeset
    fi

    # Reset PID.
    _prompt_jbulcher_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_jbulcher_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_jbulcher_precmd_async_data"

    local git_path=$(git -C "$(pwd)" rev-parse --show-prefix 2> /dev/null)
    git_path=$(prompt_jbulcher_color_path "/${git_path%?}")
    typeset -p git_path >> "$_prompt_jbulcher_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_jbulcher_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # prompt colors
  local c
  typeset -A c

  # Format PWD.
  _prompt_jbulcher_pwd=$(prompt_jbulcher_color_path "$(print -P '%~')")

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_jbulcher_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_jbulcher_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_jbulcher_git_info WINCH
  prompt_jbulcher_precmd_async &!
  _prompt_jbulcher_precmd_async_pid=$!
}

function prompt_jbulcher_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_jbulcher_precmd_async_pid=0
  _prompt_jbulcher_precmd_async_data="${TMPPREFIX}-prompt_jbulcher_data"
  _prompt_jbulcher_root='%B%(!.%F{red}#.%F{208}%%%f)%b'
  _prompt_jbulcher_return='%(?..%F{208}[%F{red}%?%F{208}]%f )'

  # colors
  local c
  typeset -A c

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_jbulcher_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format "%B$c[prompt]...%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B$c[prompt]$%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "$c[prompt]♺%f"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "%B$c[prompt]!%f%b"

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}%a✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}%A>%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}%B<%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}%d✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}%m✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}%r➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}%S✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}%U═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}%u◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' "$(coalesce '%b' '%p' '%c')%s%A%B%S%a%d%m%r%U%u" 

  # Colors
  # %F{245} gray
  # %F{250} gray
  # %F{112} green
  # %F{208} orange

  # Define prompts.
  JBPROMPT='
${_prompt_jbulcher_return}${SSH_TTY:+"%F{245}%n%F{208}%B@%b%F{250}%m%B%F{208}:%b"}${_prompt_jbulcher_pwd}${_prompt_jbulcher_root} '
  PROMPT="${JBPROMPT}"
  RPROMPT='%F{208}%*%f'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_jbulcher_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'jbulcher'
}

prompt_jbulcher_setup "$@"
